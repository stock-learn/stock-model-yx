> [查看模型](./README.md)、[查看案例](./Y03-案例.md)

# 长上影模型公式

适用范围：通达信 PC 版及 APP。

## 1. 参数

| 参数 | 最小值 | 最大值 | 缺省值 | 说明
|------|-------:|-------:|-------:|------
|   F  |      5 |     50 |      6 | 上影振幅 F%
|   B  |     50 |    100 |     60 | 上影长度占整根K线高度的比率 B%

## 2. 代码

```txt
{Y03V1_长上影_等一天再进场的策略}

{1. 上影长度至少6%以上，满足空间要求}
{2. 上影长度占整根K线振幅的60%以上}
{3. 必须为突破型的长上影}
{4. 后一K线进入影线区域，但其高点到上影高点依然有空间}
{5. 进场振幅小于5%}

M:=MAX(C,O);

{1. 上影长度至少F%以上}
C1:=H/M>=1+F/100;

{2. 上影长度占整根K线振幅的B%以上}
C2:=(H-M)/(H-L)>=B/100;

{3. 必须为突破型的长上影}
C3:=H=HHV(H,5) {是近五日高点}
  AND H/REF(H,1)>=1+F/100; {离昨天高点也满足空间要求}

{4. 长上影后一周期进入影线区域，但其高点到上影高点依然有空间}
C4:=REFX(H,1)>=M {长上影后一周期进入影线区域}
  AND H/REFX(H,1)>=1+F/100; {依然满足空间要求}

{5. 进场振幅小于5%}
C5:=REFX(H,1)/REFX(C,1)<1.05;

{综合条件}
RJ:=C1 AND C2 AND C3 AND C4 AND C5;

{主图指标：在长上影K线上方绘制 SY 字}
{T1:=IF(REFX(H,2)>REFX(H,1),'SY-IN','SY');}
{DRAWTEXT(RJ,H*1.01,T1);}

{条件选股}
RUN:RJ;
```

注：解除 “条件选股” 和 “主图指标” 下的相应代码来切换使用方式。