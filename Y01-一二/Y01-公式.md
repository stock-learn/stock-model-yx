> [查看模型](./README.md)、[查看案例](./Y01-案例.md)

# 一二模型的公式

适用范围：通达信 PC 版及 APP。

## 1. K 线组合图

![](./assets/Y01V1.png)

- 来源：688037 芯源微 2022-01-24。
- 简述：小阳加大阳反抽后缩量跌三天，第四天跌倒支撑附近，第五天上拐进。

## 3. 参数

- N：大阳当天距当前的周期数，`[4,1000]`，默认 4。
- D：回调低点的允许偏差，`[1,5]`，默认 1，表示允许偏差 ±1%。

公式的用法注释：

```
基于 688037芯源微 2022-01-21 的6K线①②结构的选股公式。
参数D为①②低点允许的偏差（±D%）。
参数N为大阳日距离最后交易日间隔的周期数。
以往选股案例（D=1时）：
1. 2022-01-21 688037-芯源微 
2. 2022-01-21 300360-炬华科技
3. 2022-05-27 002923-明德生物
```

## 4. 代码

```txt
{Y01V1_一二模型}

{排除 K 线数量不足的股票}
K_ENOUGH:=TOTALBARSCOUNT>N+20;

{N+1 日是小阳}
BOTTOM:=LLV(REF(L,N+1),20);
XIAO_YANG:=REF(C,N+1)>REF(C,N+2)  {阳线}
  AND REF(C,N+1)/REF(C,N+2)<=1.03 {涨幅<3%}
  {前 20 日一波跌，最大跌幅 >20%}
  AND HHV(REF(H,N+1),20)/BOTTOM>1.20
  {N+1 日接近一波跌的低点，偏差 ±D%}
  AND REF(L,N+1)/BOTTOM<=1+D/100
  AND REF(L,N+1)/BOTTOM>=1-D/100;

{N 天前大阳，涨幅 >6%}
DA_YANG:=REF(C,N)/REF(C,N+1)>=1.06;

{大阳后 4 天高低点连跌}
GD_LIAN_DIE:=LAST(C<REF(C,1),N-2,N-3) {收盘价连跌}
  {高点不断降低}
  AND LAST(H<REF(H,1),N-2,N-4)
  {低点不断降低}
  AND LAST(L<REF(L,1),N-2,N-4)
  {大阳后 3 天缩量}
  AND LAST(V<REF(V,1),N-2,N-3);

{N-4 日相对 N 日缩量}
N_SUB_4:=REF(V,N-4)<REF(V,N)
  {低点接近 N+1 日低点，偏差 ±D%}
  AND REF(L,N-4)/REF(L,N+1)<=1+D/100
  AND REF(L,N-4)/REF(L,N+1)>=1-D/100;

{合并条件}
RJ:=K_ENOUGH AND XIAO_YANG AND DA_YANG AND GD_LIAN_DIE AND N_SUB_4;

{条件选股}
RUN:RJ;

{主图指标：在选股日K线下方绘制①②字}
{DRAWTEXT(RJ,LOW*0.99,'①②');}
```

注：解除 “条件选股” 和 “主图指标” 下的相应代码来切换使用方式。